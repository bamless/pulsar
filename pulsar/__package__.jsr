import io

import pulsar.errors for PrettyReporter, JsonReporter
import pulsar.opt_parse as opt_parse
import pulsar.pulsar for Pulsar

fun main(args)
    var parser = opt_parse.OptParser("pulsar [options] [files...]", "A static analyzer for the J* language", [
        opt_parse.Option("out", "o", "Write errors to the specified file", 1, ["file"]),
        opt_parse.Option("json", "j", "Output errors formatted as JSON", 0),
        opt_parse.Option("no-resolve", "v", "Disable variable resolution pass", 0),
        opt_parse.Option("no-redefined-globals", "g", "Ignore redefined global variables", 0),
        opt_parse.Option("no-unreachable", "U", "Disable unreachable code pass", 0),
        opt_parse.Option("no-unused-args", "a", "Ignore unused function arguments", 0),
        opt_parse.Option("no-unused", "u", "Disable unused variable pass", 0),
        opt_parse.Option("no-check-returns", "r", "Disable return checking pass", 0),
        opt_parse.Option("no-access-check", "A", "Disable access checking pass", 0)
    ])

    var opts, files = parser.parse(args)

    var pulsar = Pulsar(opts)

    var outFile = io.File(opts["out"], "w") if opts["out"] else io.stderr
    var reporter = JsonReporter(outFile) if opts["json"] else PrettyReporter(outFile)

    try
        for var path in files
            try
                print("Analyzing {0}..." % path)

                pulsar.analyzeFile(path, reporter)
                
                var numErrors = reporter.numErrors()
                reporter.reportErrors()

                print("File {0}: {1} errors\n" % (path, numErrors))
            except io.IOException e
                io.stderr.writeln("Error {0}: {1}\n" % (path, e.err()))
            end
        end
    ensure
        reporter.close()
    end
end