import io

import pulsar.pulsar for Pulsar
import pulsar.errors for CliReporter
import pulsar.opt_parse as opt_parse

fun run(args)
    var parser = opt_parse.OptParser("pulsar [options] [files...]",  "A static analyzer for the J* language", [
        opt_parse.Option("no-unreachable", "U", opt_parse.FLAG, "Disable unreachable code pass"),
        opt_parse.Option("no-unused", "u", opt_parse.FLAG, "Disable unused variable pass"),
        opt_parse.Option("no-check-returns", "r", opt_parse.FLAG, "Disable return checking pass"),
        opt_parse.Option("no-resolve", "v", opt_parse.FLAG, "Disable variable resolution pass"),
    ])

    var opts, files = parser.parse(args)

    var pulsar = Pulsar(CliReporter(), opts)

    for var path in files
        try
            print("Analyzing {0}..." % path)
            pulsar.analyzeFile(path)
        except io.IOException e
            io.stderr.writeln("[Error] {0}" % e.err())
        end
    end
end