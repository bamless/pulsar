import io
import sys

import pulsar.errors.buffered_reporter for BufferedReporter
import pulsar.errors.json_formatter for JsonFormatter
import pulsar.errors.pretty_formatter for PrettyFormatter
import pulsar.errors.reporter for Severity
import pulsar.opt_parse for OptParser, Option
import pulsar.pulsar for Pulsar

var VERSION = (0, 0, 6)

fun main(args)
    var optParser = OptParser("pulsar [options] [files...]", "A static analyzer for the J* language", [
        Option("out", "o", "Write errors to the specified file", 1, ["file"]),
        Option("version", "v", "Print version information and exit"),
        Option("json", "j", "Output errors formatted as JSON"),
        Option("no-resolve", "V", "Disable variable resolution pass"),
        Option("no-redefined-globals", "g", "Ignore redefined global variables"),
        Option("no-unreachable", "U", "Disable unreachable code pass"),
        Option("no-unused-args", "a", "Ignore unused function arguments"),
        Option("no-unused", "u", "Disable unused variable pass"),
        Option("no-check-returns", "r", "Disable return checking pass"),
        Option("no-access-check", "A", "Disable access checking pass")
    ])

    var opts, files = optParser.parse(args)

    if opts["version"]
        print("{0} v{1}" % (__name__, VERSION.join(".")))
        sys.exit(0)
    end

    var outFile = io.stderr
    
    if opts["out"]
        try
            outFile = io.File(opts["out"], "w")
        except io.IOException e
            io.stderr.writeln("Error opening output file: {0}" % e.err())
            sys.exit(-1)
        end
    end

    var pulsar = Pulsar(opts)
    var formatter = JsonFormatter() if opts["json"] else PrettyFormatter()
    var reporter = BufferedReporter(formatter, outFile)

    try
        var totalErrs = {
            Severity.ERROR   : 0,
            Severity.WARNING : 0
        }

        for var path in files
            try
                print("Analyzing {0}..." % path)

                pulsar.analyzeFile(path, reporter)
                var numErrors = reporter.reportErrors()

                for var errorType in numErrors
                    totalErrs[errorType] += numErrors[errorType]
                end

                print("File {0}: {1} errors, {2} warnings\n" % (path, numErrors[Severity.ERROR], \
                      numErrors[Severity.WARNING]))
            except io.IOException e
                io.stderr.writeln("Error analyzing '{0}': {1}\n" % (path, e.err()))
                totalErrs[Severity.ERROR] += 1
            end
        end

        print("Total: {0} errors, {1} warnings in {2} {3}" % (totalErrs[Severity.ERROR], \
              totalErrs[Severity.WARNING], #files, "files" if #files != 1 else "file"))
    ensure
        if opts["out"]
            outFile.close()
        end
    end
end